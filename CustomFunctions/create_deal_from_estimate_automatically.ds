/*

  This is a sample Custom Function written to be used in Workflow automation of Zoho Books.
	
  This custom function automatically creates a deal in Zoho CRM when an estimate is created in Zoho Books.

  Create a connection by the name "zbooks" for Zoho Books service.

  Module               - Estimate
  Workflow Type        - Event Based
  When Estimate is     - Created
  Filter the triggers  - (Optional)
  Actions              - Type: Custom function; Name: Choose the custom function that you have created

*/

estimateID = estimate.get("estimate_id");
estimateDate = estimate.get("date").toDate();
organizationID = organization.get("organization_id");
customerID = estimate.get("customer_id");
total = estimate.get("total");
customerDetails = zoho.books.getRecordsByID("Contacts", organizationID, customerID, "zbooks").get("contact");
zcrmAccountID = customerDetails.get("zcrm_account_id");
zcrmContactID = customerDetails.get("zcrm_contact_id");
estimateDetails = zoho.books.getRecordsByID("Estimates", organizationID, estimateID, "zbooks").get("estimate");
estimate_number = estimateDetails.get("estimate_number");
expiry_date = estimateDetails.get("expiry_date");

if(expiry_date.isNull())
{
	expiry_date = estimateDate.addDay(10);
	expiry_date = expiry_date.toDate();
}
else
{
	expiry_date = expiry_date.toDate();
}

if(!zcrmAccountID.isNull() || !zcrmContactID.isNull())
{
	dealsMap = Map();
	dealsMap.put("Deal_Name", estimate_number);
	dealsMap.put("Stage", "Needs Analysis");
	dealsMap.put("Closing_Date", expiry_date);
	dealsMap.put("id", customerID);
	dealsMap.put("Contact_Name", zcrmContactID);
	dealsMap.put("Account_Name", zcrmAccountID);
	dealsMap.put("Amount", total);
	createdDeal = zoho.crm.createRecord("Deals", dealsMap);
	info createdDeal;
	dealID = createdDeal.get("id");
	json = Map();
	json.put("zcrm_potential_id", dealID);
	update = zoho.books.updateRecord("Estimates", organizationID, estimateID, json, "zbooks");
	info update.get("message");
}
else
{
	info "Cannot create a Deal in CRM";
}
